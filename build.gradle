/*
 *
 *  Copyright (C) 2010 JFrog Ltd.
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *  http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 * /
 */

import org.jfrog.build.Version
import org.jfrog.build.WriteVersionProperties

// version of the root project does not matter
version = '1'

allprojects {
    group = 'org.jfrog.wharf'
    apply plugin: 'idea'

    ideaProject {
        javaVersion = '1.6'
    }
}

subprojects {
    apply plugin: 'maven'
    apply plugin: 'java'

    version = new Version(it)
    status = version.getStatus()

    task printVersion {
        doFirst { println "Version of ${it.path} is $version with status $status" }
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.java
    }

    artifacts {
        archives sourceJar
    }

    task versionProperties(type: WriteVersionProperties) {
        propertiesFile = new File(sourceSets.main.classesDir, "org/jfrog/wharf/${project.name}.properties")
    }

    classes.dependsOn(versionProperties)
}

project('wharf-resolver') {
    description = 'Basic client configuration classes for resolution'
}

project('wharf-core') {
    description = 'Ivy cache system for multi-resolvers'

    dependencies {
        compile group: 'org.apache.ant', name: 'ant', version: '1.8.1'
        compile group: 'org.codehaus.jackson', name: 'jackson-core-asl', version: '1.5.6'
        compile group: 'org.codehaus.jackson', name: 'jackson-mapper-asl', version: '1.5.6'
        compile group: 'com.esotericsoftware', name: 'kryo', version: '1.03'
        compile group: 'com.esotericsoftware', name: 'minlog', version: '1.2'
        compile group: 'com.esotericsoftware', name: 'reflectasm', version: '1.0'
        compile group: 'asm', name: 'asm', version: '3.2'
        compile group: 'org.apache.ivy', name: 'ivy', version: '2.2.0'
        testCompile group: 'junit', name: 'junit', version: '4.8.1'
    }
}
