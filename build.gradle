/*
 *
 *  Copyright (C) 2010 JFrog Ltd.
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *  http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 * /
 */

import org.jfrog.build.Version
import org.jfrog.build.WriteVersionProperties

// version of the root project does not matter
version = '1'

allprojects {
    group = 'org.jfrog.wharf'
    apply plugin: 'idea'
    apply plugin: 'artifactory'

    idea {
        project {
            javaVersion = '1.6'
        }
    }
}

artifactory {
    //contextUrl = 'http://fredsvaio:8081/artifactory'
    contextUrl = 'http://repo.jfrog.org/artifactory'
    publish {
        defaults {
            publishConfigs('extraPublish', 'archives')
            properties = ['build.status': "$it.project.status".toString()]
            publishIvy = true
            publishPom = false
        }
    }
    resolve {
        repository {
            repoKey = 'gradle'
        }
    }
}

subprojects {
    apply plugin: 'maven'
    apply plugin: 'java'

    version = new Version(it)
    status = version.getStatus()

    configurations {
        extraPublish
    }

    task printVersion {
        doFirst { println "Version of ${it.path} is $version with status $status" }
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.java
    }

    artifacts {
        extraPublish sourceJar
    }

    artifactoryPublish {
        dependsOn: build
    }

    task versionProperties(type: WriteVersionProperties) {
        //For 1.0-m-4
        //propertiesFile = new File(sourceSets.main.output.classesDir, "org/jfrog/wharf/${project.name}.properties")
        propertiesFile = new File(sourceSets.main.classesDir, "org/jfrog/wharf/${project.name}.properties")
    }

    uploadArchives {
        uploadDescriptor = true
    }

    classes.dependsOn(versionProperties)
}

project('layout-mapper') {
    apply plugin: 'groovy'

    description = 'Support artifact to path repository layout map and transform'

    dependencies {
        compile group: 'com.google.guava', name: 'guava', version: 'r09'
        compile group: 'commons-lang', name: 'commons-lang', version: '2.6'
        compile group: 'javax.annotation', name: 'jsr250-api', version: '1.0'
        groovy 'org.codehaus.groovy:groovy-all:1.7.10@jar'
        testCompile group: 'junit', name: 'junit', version: '4.8.1'
        testCompile group: 'org.spockframework', name: 'spock-core', version: '0.5-groovy-1.7'
    }
}

project('wharf-repository') {
    description = 'Support repositories definition for artifact retrieval and caching'

    dependencies {
        compile project(':layout-mapper')
        compile group: 'com.google.guava', name: 'guava', version: 'r09'
        compile group: 'commons-lang', name: 'commons-lang', version: '2.6'
        testCompile group: 'junit', name: 'junit', version: '4.8.1'
    }
}

project('wharf-resolver') {
    apply plugin: 'groovy'

    description = 'Integration of Ivy DependencyResolver with Wharf'

    dependencies {
        compile project(':wharf-repository')
        compile group: 'org.apache.ant', name: 'ant', version: '1.8.1'
        compile group: 'com.esotericsoftware', name: 'kryo', version: '1.03'
        compile group: 'com.esotericsoftware', name: 'minlog', version: '1.2'
        compile group: 'com.esotericsoftware', name: 'reflectasm', version: '1.0'
        compile group: 'asm', name: 'asm', version: '3.2'
        compile group: 'org.apache.ivy', name: 'ivy', version: '2.2.0'
        groovy 'org.codehaus.groovy:groovy-all:1.7.10@jar'
        testCompile group: 'junit', name: 'junit', version: '4.8.1'
        testCompile group: 'org.spockframework', name: 'spock-core', version: '0.5-groovy-1.7'
    }
}


